#!/bin/sh
# Tcl ignores the next line -*- tcl -*- \
    exec wish "$0" -- "$@"

package require Tk
package require ctext


################################################################
# functions

proc not_implemented {title} {
	tk_messageBox -title $title -message "command not implemented"
}

# exit program
proc do_quit {} {
	# workaround:
	# without `wm withdraw .', x11 lost key input when program exit
	wm withdraw .
	destroy .
}

# read file and insert its content into text widget
proc read_file {w name} {
	global filename
	if {!([file isfile $name] && [file readable $name])} {
		return
	}
	set filename $name
	set_file_number
	set f [open $filename r]

	# erase previous content
	$w configure -state normal
	$w delete 1.0 end

	# cancel previous open file event
	foreach i [after info] {puts [after cancel $i]}

	# read asynchronous
	read_file_internal $w $f
}

# this procedure is called by read_file
proc read_file_internal {w f} {
	if {![eof $f]} {
		# save position
		set pos [$w index insert]
		$w configure -state normal
		$w insert end [read $f 1024]
		$w configure -state disabled
		# restore position
		$w mark set insert $pos
		after 10 "read_file_internal $w $f"
	} else {
		close $f
	}
}

proc get_ready {} {
	global preceding_number
	focus .ctext.t
	set preceding_number ""
}

# cancel action, ready to command
proc do_cancel {} {
	get_ready
}

# append digit to precedence number
proc do_number {d} {
	global preceding_number
	set preceding_number [string cat $preceding_number $d]
}

# open (examine) new file
proc do_examine {} {
	read_file .ctext [.file.name get]
	get_ready
}

# set file label "current/max"
proc set_file_number {} {
	set cur [expr 1 + [.file.name current]]
	set max [llength [.file.name cget -values]]
	.file.number configure -text "$cur/$max"
}

# return preceding number or default
# d: if no preceding_number, return d
proc get_precedence {d} {
	global preceding_number
	if {$preceding_number ne ""} {
		return $preceding_number
	} else {
		return $d
	}
}

# scroll line.
# cursor(insert) is moved also.
# sign: + or -
proc scroll_line {sign} {
	global preceding_number
	set w .ctext

	set num [get_precedence 1]

	set pos [expr [$w index insert] $sign $num]
	$w mark set insert $pos
	$w yview insert
	get_ready
}

# scroll lines forward
proc do_forward_line {} {
	scroll_line +
}

# scroll lines backward
proc do_backward_line {} {
	scroll_line -
}

################################################################
# variables

# number, used by various command
set preceding_number ""

# current file name
set filename ""

################################################################
# main window

# filename widget
frame .file
ttk::combobox .file.name -textvariable filename
label .file.number -text "0/0"
pack .file.number .file.name -side left
pack .file

# debug tcl command widget
frame .tcl
label .tcl.l -text {run tcl command: }
entry .tcl.e -textvariable tclscript
pack .tcl.l .tcl.e -side left
pack .tcl
bind .tcl.e <Return> { eval $tclscript }

# text widget
ctext .ctext -yscrollcommand ".scroll set" -setgrid true -wrap none
ttk::scrollbar .scroll -command ".ctext yview"
pack .scroll -side right -fill y
pack .ctext -expand yes -fill both
.ctext configure -state disabled

################################################################
# commands

# exit program
bind .ctext <q> do_quit
bind .ctext <Q> do_quit
bind .ctext <colon><q> do_quit
bind .ctext <colon><Q> do_quit
bind .ctext <Z><Z> do_quit

# help
bind .ctext <h> {not_implemented help}
bind .ctext <H> {not_implemented help}

# scrolling
bind .ctext <j> {do_forward_line}
bind .ctext <k> {do_backward_line}
bind .ctext <f> {not_implemented forward_window}
bind .ctext <b> {not_implemented backward_window}

# cancel
bind . <Escape> do_cancel

# number
bind .ctext 0 {do_number %K}
bind .ctext 1 {do_number %K}
bind .ctext 2 {do_number %K}
bind .ctext 3 {do_number %K}
bind .ctext 4 {do_number %K}
bind .ctext 5 {do_number %K}
bind .ctext 6 {do_number %K}
bind .ctext 7 {do_number %K}
bind .ctext 8 {do_number %K}
bind .ctext 9 {do_number %K}

# file open prompt
bind .ctext <colon><e> {focus .file.name}

# open (examine) new file
bind .file.name <Return> {do_examine}

# run tcl command
bind .ctext <exclam> "focus .tcl.e"

################################################################
# program start

# fill combobox with $argv
.file.name configure -values $argv

# read first argument
set filename [lindex [.file.name cget -values] 0]
read_file .ctext $filename

# wait the command
focus .ctext.t

# Local variables:
# indent-tabs-mode: t
# tab-width: 4
# End:
